plugins {
    id 'java'
}

repositories {
    jcenter()
}

defaultTasks('test')

dependencies {
    testImplementation group: 'org.testng', name: 'testng', version: '7.3.+'
    testImplementation group: 'io.cucumber', name: 'cucumber-testng', version: '6.9.+'
    testImplementation group: 'io.cucumber', name: 'cucumber-java', version: '6.9.+'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '4.0.+'
    testImplementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '4.3.+'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testImplementation group: 'org.apache.commons', name: 'commons-configuration2', version: '2.7'

    testRuntimeOnly group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.+'
}

java {
    compileTestJava.options.encoding = 'UTF-8'
}

test.finalizedBy('hightPriorityRegressionTests', 'lowPriorityRegressionTests')

task hightPriorityRegressionTests(type: Test) {
    useTestNG() {
        suites 'src/test/resources/com/mitigram/web/testngrunners/highpriorityregressiontests.xml'
    }
}

task lowPriorityRegressionTests(type: Test) {
    useTestNG() {
        suites 'src/test/resources/com/mitigram/web/testngrunners/lowpriorityregressiontests.xml'
    }
}

tasks.withType(Test) {
    testLogging {
        events  = ['PASSED', 'FAILED', 'SKIPPED']
    }
    outputs.upToDateWhen { false }
    failFast(true)
}
